plugins {
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'com.adarshr.test-logger' version '2.1.0'
    id 'groovy'
}

apply plugin: 'groovy'
apply plugin: 'io.spring.dependency-management'

group = 'org.cugos'
version = '0.6.0-SNAPSHOT'
sourceCompatibility = '1.8'

jar {
    baseName = 'geopackageserver'
}

sourceSets {
    e2e {
        groovy.srcDir    file('src/e2e/groovy')
        resources.srcDir file('src/e2e/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath + configurations.e2eRuntime
    }
}

task e2e(type: Test) {
    systemProperty "webdriver.chrome.driver", System.getProperty("webdriver.chrome.driver")
    systemProperty "webdriver.gecko.driver", System.getProperty("webdriver.gecko.driver")
    testClassesDirs = sourceSets.e2e.output.classesDirs
    classpath = sourceSets.e2e.runtimeClasspath
}

tasks.withType(Test) {
    systemProperty "org.geotools.referencing.forceXY", "true"
}

repositories {
    maven {
        url "https://repo.osgeo.org/repository/release"
    }
    maven {
        url "https://repo.osgeo.org/repository/snapshot"
    }
    mavenCentral()
}

bootRun {
    if ( project.hasProperty('args') ) {
        args project.args.split('\\s+')
    } else {
        args "--file=data.gpkg"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-freemarker'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.codehaus.groovy:groovy'
    implementation("org.geoscript:geoscript-groovy:1.14.0") {
        exclude module: "gt-jdbc-postgis"
        exclude module: "gt-jdbc-h2"
        exclude module: "gt-jdbc-mysql"
        exclude module: "gt-jdbc-spatialite"
        exclude module: "gt-netcdf"
        exclude module: "gt-charts"
        exclude module: "gt-swing"
        exclude module: "gt-geotiff"
        exclude module: "gt-image"
        exclude module: "gt-imagemosaic"
        exclude module: "gt-arcgrid"
        exclude module: "gt-grassraster"
        exclude module: "gt-gtopo30"
        exclude module: "gt-imagepyramid"
        exclude module: "gt-imageio-ext-gdal"
        exclude module: "gt-mbtiles"
        exclude module: "gdal"
        exclude module: "gt-ogr-jni"
    }
    implementation "io.springfox:springfox-swagger2:2.9.2"
    implementation "io.springfox:springfox-swagger-ui:2.9.2"

    testImplementation("junit:junit")
    testImplementation("org.springframework.boot:spring-boot-starter-test")

    e2eRuntime configurations.testRuntime
    e2eImplementation sourceSets.main.output
    e2eImplementation configurations.testImplementation
    e2eImplementation sourceSets.test.output
    e2eImplementation "org.seleniumhq.selenium:selenium-java"
    e2eImplementation "org.seleniumhq.selenium:selenium-chrome-driver"
    e2eImplementation "org.seleniumhq.selenium:selenium-firefox-driver"
    e2eImplementation "org.seleniumhq.selenium:selenium-support"
    e2eImplementation 'com.google.code.gson:gson:2.8.5'
}
